yalle {
    Program     = Statement+

    Statement   = VarDecl
                | TypeDecl
                | FunDecl
                | Exp7 "=" Exp ";"                              --assign
                | break ";"                                     --break
                | return Exp ";"                                --return
                | return ";"                                    --shortreturn
                | IfStmt
                | LoopStmt
                | print Exp ";"                                 --print
    // changes vs Carlos (to correct for in analyzer)
    // - VarDecl has VarType
    VarDecl     = let VarType id "-=" Exp ";"
    TypeDecl    = struct id "-x-x-x-x-" Field* "-x-x-x-x-"
    Field       = "|" id ":" Type "|"
    FunDecl     = function id Params (":" Type)? Block
    Params      = "(" ListOf<Param, ","> ")"
    Param       = id ":" Type
    Type        = "[" Type "]"                                  --array
                | "(" ListOf<Type, ","> ")" "->" Type           --function
                | id                                            --id
    VarType     = id
    IfStmt      = if Exp Block else Block                       --long
                | if Exp Block else IfStmt                      --elsif
                | if Exp Block                                  --short
    LoopStmt    = while Exp Block                               --while
                | repeat Exp Block                              --repeat
                // Lose this one? Not sure what it means
                | for id in Exp Block                           --collection
    Block       = "~~{" Statement* "}"

    Exp         = Exp1 "?" Exp1 ":" Exp                         --conditional
                | Exp1
    Exp1        = Exp2 ("||" Exp2)+                             --or
                | Exp2 ("&&" Exp2)+                             --and
                | Exp2
    Exp2        = Exp3 ("<="|"<"|"=="|"!="|">="|">") Exp3       --compare
                | Exp3
    Exp3        = Exp3 ("<<" | ">>") Exp4                       --shift
                | Exp4
    Exp4        = Exp4 ("+" | "-") Exp5                         --add
                | Exp5
    Exp5        = Exp5 ("*"| "/" | "%") Exp6                    --multiply
                | Exp6
    Exp6        = Exp7 "**" Exp6                                --power
                | Exp7
                | ("#" | "-" | "!" ) Exp7        --unary
    Exp7        = true ~mut
                | false ~mut
                | floatlit ~mut
                | intlit ~mut
                | stringlit ~mut
                | Exp7 ("(" | "?(") ListOf<Exp, ","> ")" ~mut   --call
                | Exp7 ("[" | "?[") Exp "]"                     --subscript
                | Exp7 ("." | "?.") id                          --member
                | id                                            --id
                | Type_array "(" ")" ~mut                       --emptyarray
                | "[" NonemptyListOf<Exp, ","> "]" ~mut         --arrayexp
                | "(" Exp ")" ~mut                              --parens

    intlit      = digit+
    floatlit    = digit+ "." digit+ (("E" | "e") ("+" | "-")? digit+)?
    stringlit   = "\"" char* "\""
    char        = ~"\\" ~"\"" any
                | "\\" ("n" | "t" | "\"" | "\\")                --escape
    mut         = ~"==" "=" | "++" | "--"

    let         = "brand" ~alnum
    struct      = "ranch" ~alnum
    function    = "task" ~alnum
    if          = "iffin" ~alnum
    else        = "else" ~alnum
    while       = "otherwise" ~alnum
    repeat      = "repeat" ~alnum
    for         = "for" ~alnum
    in          = "in" ~alnum
    break       = "whoa" ~alnum
    print       = "holler" ~alnum
    return      = "roundup" ~alnum
    true        = "true" ~alnum
    false       = "false" ~alnum
    keyword     = let | struct | function | if | else | while | repeat
                | for | in | break | return | true | false
    id          = ~keyword letter alnum*

    space      += "//" (~"\n" any)*                             --comment
}